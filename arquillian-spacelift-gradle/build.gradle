apply plugin: 'groovy'
apply plugin: 'maven-publish'
apply plugin: 'maven-publish-auth'

group 'org.arquillian.spacelift.gradle'
version '1.0.0-alpha-1'

repositories {
    mavenCentral()
    // adding staging repository, might be handy for lastest Arquillian Native version
    maven {
        name 'jboss-staging-repository-group'
        url 'https://repository.jboss.org/nexus/content/groups/staging'
    }
}

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.hibernate.build.gradle:gradle-maven-publish-auth:2.0.1'
    }
}

configurations {
    junitreport
}

dependencies {
    compile gradleApi()
    compile localGroovy()

    // commons lang is used to do platform checks
    compile 'org.apache.commons:commons-lang3:3.1'

    // Spacelift abstracts execution of external commands
    compile 'org.arquillian.spacelift:arquillian-spacelift:1.0.0.Alpha2'
    compile 'org.jboss.aerogear.test:spacelift-jboss-manager:0.2.0'

    // Ant is used to provide junitreport
    //compile 'org.apache.ant:ant-junit:1.9.4'
    junitreport 'org.apache.ant:ant-junit:1.9.4'
    
    testCompile 'junit:junit:4.11'
}


// publish plugin to Maven Repository
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourcesJar {
                classifier "sources"
            }

            pom.withXml {
                def Node root = asNode();

                //node.appendNode('groupId', 'org.arquillian.spacelift.gradle')
                //node.appendNode('artifactId', 'spacelift-plugin')
                //node.appendNode('version', project.version)


                root.appendNode('name', 'Gradle Plugin for Arquillian Spacelift')
                root.appendNode('description', 'Gradle Plugin to provide test environment')
                root.appendNode('url', 'https://github.com/aerogear/aerogear-testing-tools/tree/master/arquillian-spacelift-gradle')

                def org = root.appendNode( 'organization' )
                org.appendNode( 'name', 'Arquillian.org' )
                org.appendNode( 'url', 'https://arquillian.org' )

                def issues = root.appendNode( 'issueManagement' )
                issues.appendNode( 'system', 'JIRA' )
                issues.appendNode( 'url', 'https://issues.jboss.org/browse/ARQ' )

                def scm = root.appendNode( 'scm' )
                scm.appendNode( 'url', 'https://github.com/aerogear/aerogear-testing-tools' )
                scm.appendNode( 'connection', 'scm:git:https://github.com/aerogear/aerogear-testing-tools.git' )
                scm.appendNode( 'developerConnection', 'scm:git:git@github.com:aerogear/aerogear-testing-tools.git' )

                def license = root.appendNode( 'licenses' ).appendNode( 'license' )
                license.appendNode( 'name', 'Apache License, Version 2.0' )
                license.appendNode( 'url', 'http://www.apache.org/licenses/LICENSE-2.0.txt' )
                license.appendNode( 'distribution', 'repo' )

                def devs = root.appendNode( 'developers' )
                def kpiwko = devs.appendNode( 'developer' )
                kpiwko.appendNode( 'name', 'Karel Piwko' )
                kpiwko.appendNode( 'email', 'kpiwko@redhat.com' )
                def smikloso = devs.appendNode( 'developer' )
                smikloso.appendNode( 'name', 'Stefan Miklosovic' )
                smikloso.appendNode( 'email', 'smikloso@redhat.com' )
                def tkriz = devs.appendNode( 'developer' )
                tkriz.appendNode( 'name', 'Tadeas Kriz' )
                tkriz.appendNode( 'email', 'tkriz@redhat.com' )

                // TEMPORARY : currently Gradle Publishing feature is exporting dependencies as 'runtime' scope,
                // rather than 'compile'; fix that.
                asNode().dependencies[0].dependency.each {
                    it.scope[0].value = 'compile'
                }
            }
        }
    }

    repositories {
        maven {
            if ( project.version.endsWith( "SNAPSHOT" ) ) {
                name 'jboss-snapshots-repository'
                url 'https://repository.jboss.org/nexus/content/repositories/snapshots/'
            }
            else {
                name 'jboss-releases-repository'
                url 'https://repository.jboss.org/nexus/service/local/staging/deploy/maven2/'
            }
        }
    }
}

// add junitreport task to Ant classpath
ClassLoader antClassLoader = org.apache.tools.ant.Project.class.classLoader                                                                                                                                   
configurations.junitreport.each { File f ->
    antClassLoader.addURL(f.toURI().toURL())
} 

// construct package with sources
task sourcesJar(type: Jar, dependsOn: compileGroovy) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.8'
}



