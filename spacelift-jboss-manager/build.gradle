import org.arquillian.spacelift.gradle.*
import org.arquillian.spacelift.Spacelift
import org.arquillian.spacelift.gradle.maven.*
import org.arquillian.spacelift.process.CommandBuilder
import java.io.File

defaultTasks 'test'
apply plugin: 'spacelift'

ext {
    defaultWildflyVersion = "8.2.0.Final"
    defaultJbossAS7Version = "7.1.1.Final"
    defaultJbossAS7VersionMajor = "7.1"

    // this is a holder of tested containers
    containerEnvs = []
}

spacelift {

    workspace = new File(project.rootDir, ".ws")
    installationsDir = new File(workspace, "installations")

    tools {
        mvn {
            command {
                def m2 = System.getenv("M2")
                def m2_home = System.getenv("M2_HOME")

                if (m2 != null && !m2.isEmpty()) {
                    return new CommandBuilder("${m2}/mvn")
                } else if (m2_home != null && !m2_home.isEmpty()) {
                    return new CommandBuilder("${m2_home}/bin/mvn")
                } else {
                    return new CommandBuilder("mvn")
                }
            }
        }
    }

    profiles {
        'default' {
            tests 'jbossManagerTest'
            enabledInstallations 'wildfly', 'as7', 'as7WithSpaces' 
        }
    }

    installations {
        wildfly {
            product 'jbossManager'
            version { project.wildflyVersion }
            remoteUrl { "http://download.jboss.org/wildfly/${project.wildflyVersion}/wildfly-${project.wildflyVersion}.tar.gz" }
            home { "wildfly-${project.wildflyVersion}" }
            isInstalled {
                // FIXME abusing isInstalled closure to add container to test against
                project.containerEnvs << [home:"${home.absolutePath}", type:"WILDFLY"]
                return home.exists()
            }
            postActions {
                project.ant.chmod(dir: "${home.absolutePath}/bin", perm:"a+x", includes:"**/*.sh")
            }
        }
        as7 {
            product 'jbossManager'
            version { project.jbossAS7Version }
            remoteUrl { "http://download.jboss.org/jbossas/${project.jbossAS7VersionMajor}/jboss-as-${project.jbossAS7Version}/jboss-as-${project.jbossAS7Version}.zip" }
            home { "jboss-as-${project.jbossAS7Version}" }
            isInstalled {
                // FIXME abusing isInstalled closure to add container to test against
                project.containerEnvs << [home: "${home.canonicalPath}", type: "AS7"]
                return home.exists()
            }
            postActions {
                project.ant.chmod(dir: "${home.absolutePath}/bin", perm:"a+x", includes:"**/*.sh")
            }
        }
        as7WithSpaces {
            product 'jbossManager'
            version { project.jbossAS7Version }
            remoteUrl { "http://download.jboss.org/jbossas/${project.jbossAS7VersionMajor}/jboss-as-${project.jbossAS7Version}/jboss-as-${project.jbossAS7Version}.zip" }
            home { "jboss-as- -${project.jbossAS7Version}" }
            extractMapper { toDir(home).cutdirs() }
            isInstalled {
                // FIXME abusing isInstalled closure to add container to test against
                project.containerEnvs << [home: "${home.canonicalPath}", type:"AS7"]
                return home.exists()
            }
            postActions {
                project.ant.chmod(dir: "${home.absolutePath}/bin", perm:"a+x", includes:"**/*.sh")
            }
        }
    }

    tests {
        jbossManagerTest {

            dataProvider {
                project.containerEnvs
            }

            execute { Map containerEnv ->
                Spacelift.task(MavenExecutor).pom("${project.rootDir}/pom.xml")
                    .goals('clean', 'test')
                    .property("jboss.home=${containerEnv.home}")
                    .property("containerType=${containerEnv.type}")
                    .execute().await()
            }
        }
    }
}

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'org.arquillian.spacelift.gradle:arquillian-spacelift-gradle:1.0.0-alpha-12'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.2'
}
